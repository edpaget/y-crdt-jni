plugins {
    id 'java-library'
    id 'checkstyle'
    id 'maven-publish'
}

group = 'net.carcdr'
version = rootProject.version

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.author = true
    options.version = true
    options.links = [
        'https://docs.oracle.com/en/java/javase/21/docs/api/'
    ]
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    // For CrossImplementationSyncTest - requires both implementations on classpath
    testImplementation project(':ycrdt-jni')
    testImplementation project(':ycrdt-panama')
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
        showStandardStreams = false
    }
    // Enable native access for Panama FFM API (CrossImplementationSyncTest uses both)
    jvmArgs '--enable-native-access=ALL-UNNAMED'
}

checkstyle {
    toolVersion = '10.12.5'
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    ignoreFailures = false
    maxWarnings = 0
}

checkstyleMain {
    source = 'src/main/java'
}

checkstyleTest {
    source = 'src/test/java'
}

tasks.named('check') {
    dependsOn 'checkstyleMain'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'ycrdt-core'
            from components.java

            pom {
                name = 'Y-CRDT Core'
                description = 'Core interfaces for Y-CRDT Java bindings'
                url = 'https://github.com/edpaget/y-crdt-jni'

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }

                developers {
                    developer {
                        id = 'edpaget'
                        name = 'Edward Paget'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/edpaget/y-crdt-jni.git'
                    developerConnection = 'scm:git:ssh://github.com/edpaget/y-crdt-jni.git'
                    url = 'https://github.com/edpaget/y-crdt-jni'
                }
            }
        }
    }
}
