// Root project build configuration for multi-module Y-CRDT JNI project

allprojects {
    group = 'net.carcdr'
    version = findProperty('projectVersion') ?: '0.1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'

    repositories {
        mavenCentral()
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    checkstyle {
        toolVersion = '10.12.5'
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
        ignoreFailures = false
        maxWarnings = 0
    }

    // Configure GitHub Packages repository for all modules with maven-publish
    plugins.withId('maven-publish') {
        publishing {
            repositories {
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/edpaget/y-crdt-jni")
                    credentials {
                        username = System.getenv("GITHUB_ACTOR") ?: findProperty("gpr.user")
                        password = System.getenv("GITHUB_TOKEN") ?: findProperty("gpr.key")
                    }
                }
            }
        }
    }
}
